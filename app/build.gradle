plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

def release_store_password
def release_key_password
def release_key_alias
def keystoreProperties = new Properties()
if (rootProject.file("keystore.properties").exists()) {
    keystoreProperties.load(new FileInputStream(rootProject.file("keystore.properties")))
    release_store_password = keystoreProperties['RELEASE_STORE_PASSWORD']
    release_key_password = keystoreProperties['RELEASE_KEY_PASSWORD']
    release_key_alias = keystoreProperties['RELEASE_KEY_ALIAS']
} else {
    release_store_password = System.env.RELEASE_STORE_PASSWORD
    release_key_password = System.env.RELEASE_KEY_PASSWORD
    release_key_alias = System.env.RELEASE_KEY_ALIAS
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    signingConfigs {
        release {
            storeFile file("justdo.jks")
            storePassword = release_store_password
            keyAlias = release_key_alias
            keyPassword = release_key_password
        }
    }

    defaultConfig {
        applicationId "com.domore.justdo"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix "-debug"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding = true
    }
}

dependencies {

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def moxy_version = "2.2.2"
    def cicerone_version = "7.0"
    def retrofit_version = "2.9.0"
    def okhttp_version = "4.9.1"
    def room_version = "2.3.0"
    def dagger_version = '2.37'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    //moxy
    implementation "com.github.moxy-community:moxy:$moxy_version"
    implementation "com.github.moxy-community:moxy-ktx:$moxy_version"
    implementation "com.github.moxy-community:moxy-androidx:$moxy_version"
    kapt "com.github.moxy-community:moxy-compiler:$moxy_version"
    //cicerone
    implementation "com.github.terrakok:cicerone:$cicerone_version"
    //RxJava
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.13'
    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava3:$retrofit_version"
    //Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    //OkHTTP
    implementation(platform("com.squareup.okhttp3:okhttp-bom:$okhttp_version"))
    implementation "com.squareup.okhttp3:okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor"
    //Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"
    //Dagger
    implementation "com.google.dagger:dagger:$dagger_version"
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}